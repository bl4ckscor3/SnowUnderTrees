plugins {
	id 'eclipse'
	id 'idea'
	id 'net.minecraftforge.gradle' version '6.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.4.4'
group = 'bl4ckscor3.mod.snowundertrees'

base {
	archivesName = 'snowundertrees-1.20.1'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))


minecraft {
	mappings channel: 'parchment', version: '2023.09.03-1.20.1'
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	copyIdeResources = true

	
    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.console.level', 'debug'
            //fixes refmap of deps
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                snowundertrees {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.console.level', 'debug'
            args '-nogui'
            //fixes refmap of deps
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                snowundertrees {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.20.1-47.1.3'

    implementation fg.deobf("curse.maven:eternal-winter-273891:4711122")
    implementation fg.deobf("curse.maven:kiwi-303657:5220837") //snow real magic dependency
    implementation fg.deobf("curse.maven:serene-seasons-291874:4761603")
    implementation fg.deobf("curse.maven:snow-real-magic-308663:5232560")
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title": "Snow Under Trees",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "Snow Under Trees",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3"])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}